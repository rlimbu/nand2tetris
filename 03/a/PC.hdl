// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Put your code here:
    // Completed: 2018-04-06
	
    // increment output
    Inc16(in=outpc, out=outinc);
	
    // select output based on control bits 
    Mux8Way16(a=outpc, b=false, c=in, d=false, e=outinc, f=false, g=in, h=false, sel[0]=reset, sel[1]=load, sel[2]=inc, out=outmux8way);
	
    // populate PC output pin via a register so that feedback loop works 
    Register(in=outmux8way, load=true, out=out, out=outpc);	
	
}
